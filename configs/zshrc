# Path to your oh-my-zsh configuration.
ZSH=$HOME/.oh-my-zsh

# Set my terminal if I'm not in tmux.
[[ "$TMUX" == "" ]] && TERM=xterm-256color

# Set name of the theme to load.
ZSH_THEME="bbs"

# Disable autosetting terminal title.
DISABLE_AUTO_TITLE="true"

# Disable command autocorrection.
DISABLE_CORRECTION="true"

# Display red dots while waiting for completion.
COMPLETION_WAITING_DOTS="true"

# Disable marking untracked files under VCS as dirty. This makes repository 
# status check for large repositories much, much faster.
DISABLE_UNTRACKED_FILES_DIRTY="true"

# Load these oh-my-zsh plugins.
plugins=(git colorize themes)

source $ZSH/oh-my-zsh.sh

# Configure zsh options #######################################################
setopt AUTO_LIST        # Display completion candidates immediately.
setopt CDABLE_VARS      # When an argument should be a dir but is not one,
                        # expand it as if it started with ~ (see below).
setopt AUTO_NAME_DIRS   # Any parameter that is set to an abosolute directory
                        # name automatically becomes a name for that directory
                        # in the form ~param.
setopt HIST_IGNORE_DUPS # Do not record a command in the history if it is a
                        # duplicate of the previous one.
setopt LIST_TYPES       # When listing files that are possible completions,
                        # indicate their types with a trailing character.
setopt MENU_COMPLETE    # On an ambiguous completion, insert the first match
                        # immediately rather than waiting for another tab.
setopt NO_LIST_BEEP     # No bell on ambiguous completion!!

# Adjust widget behaviors. ####################################################
# While selecting from menu completion, pressing enter accepts the selection 
# and executes the command.
bindkey -M menuselect '^M' .accept-line

# Customize to your needs...
export PATH=/usr/local/bin:/usr/local/sbin:/usr/bin:/usr/sbin:/bin:/sbin:/usr/texbin:$HOME/.vim/bundle/vim-committed/bin:$PATH
export PATH="$HOME/.rbenv/bin:$PATH"
eval "$(rbenv init -)"

# Perform a visual diff of conflicted source files in vim.
function vdiff () {
	if [ $# -eq 0 ]; then
		echo "Usage: vdiff FILENAME"
		return 1
	else
		BASE_FILE="$1"
		MINE_FILE="$1.working"

		if [ ! -f "$MINE_FILE" ]; then
			echo "The base file given does not have a .mine variation."
			return 1
		fi

		# Get all of the file variations and find the numbered ones.
    FILES=$(ls $1.merge-*.r*)
    files=("${(f)FILES}")
		#read V1_FILE V2_FILE <<< $(ls -1 $1.merge-*.r*)

    if [[ $#files -lt 2 ]]; then
			echo "I couldn't find revision variations ($1.merge-*.r*)."
			return 1
    fi

		#if [ ! -f "$V1_FILE" ] || [ ! -f "$V2_FILE" ]; then
		#	echo "I can't find revision variations ($1.r*)."
		#	return 1
		#fi

		# Re-shuffle them if they're not in numerical order.
		if [[ "${files[1]##*.r}" -gt "${files[2]##*.r}" ]]; then
			LEFT_FILE=$files[2]
			RIGHT_FILE=$files[1]
		else
			LEFT_FILE=$files[1]
			RIGHT_FILE=$files[2]
		fi

		#echo "Starting vimdiff with $LEFT_FILE -> $MINE_FILE -> $RIGHT_FILE"
		#vim -d "$LEFT_FILE" "$MINE_FILE" "$RIGHT_FILE"
		vim "$1" -c "split $RIGHT_FILE | diffthis | vert split $MINE_FILE | diffthis | vert split $LEFT_FILE | diffthis"
	fi
}

# In case you want to see a new MOTD, you can just call this function.
function motd () {
  # Set up some ANSI colors real quick.
  WHITE="\e[38;5;255m"
  GRAY="\e[38;5;239m"
  SILVER="\e[38;5;245m"

  # Output a fortune on login if the program exists.
  # This generates a very elaborate box. Perl is used to pad each line of fortune
  # with spaces so that the right edge of the box aligns. This proved to be
  # difficult if not impossible to do with awk/sed. I obviously hold my ASCII
  # boxes in very high regard.
  which fortune > /dev/null && which sed > /dev/null
  if (( $? == 0 )); then
    HEAD="╡ ${WHITE}M${SILVER}essage ${WHITE}O${SILVER}f ${WHITE}T${SILVER}he ${WHITE}D${SILVER}ay${GRAY} ╞"
    TOP_LEN=$(( $COLUMNS - 32 ))
    BOTTOM_LEN=$(( $COLUMNS - 10 ))
    TOP_LINE=${(l:$TOP_LEN::═:)}
    BOTTOM_LINE=${(l:$BOTTOM_LEN::═:)}

    echo "$WHITE╒═$SILVER════$GRAY$TOP_LINE$HEAD$SILVER═$WHITE═╕"
    fortune -s | perl -e 'my $cols=shift @ARGV; while (<>) { chomp; s/\t/    /g; print "│ ".$_.(" " x ($cols-length()-4))."│\n"; }' ${COLUMNS}
    echo "$WHITE╘═$SILVER═$GRAY$BOTTOM_LINE$SILVER════$WHITE═╛"
  fi
}

# Only for work.
if [[ "$MY_LOCATION" == "work" ]]; then
	if which ack-grep > /dev/null 2> /dev/null; then
		alias ack="ack-grep --pager='less'"
	fi

	alias svnc="svn-color.py"
	export PATH=$PATH:~/bin/svnkit-1.7.9/bin
	export PATH=$PATH:$HOME/.vim/bundle/commit-tracker/bin
	export PATH=$PATH:$HOME/bin/rbstats
  export WROOT=/usr/local/www
  export DATA=/usr/local/www/_data
  export RES=/usr/local/www/_resources
fi

# OS X things.
if [[ `uname` == "Darwin" ]]; then
	# This is really only for OS X, so don't use it at work.
	function top() {
		if [ $# -eq 0 ]; then
			/usr/bin/top -o cpu
		else
			/usr/bin/top "$@"
		fi
	}
fi

# Show an MOTD on login :-)
motd

# vim: set et ts=2 sw=2 tw=79 cc=+1 :
